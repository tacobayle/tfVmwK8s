data "template_file" "network_nfs" {
  count            = var.dhcp == false ? 1 : 0
  template = file("templates/network.template")
  vars = {
    if_name = var.nfs.if_name
    ip4 = split(",", replace(var.nodes_ip4_addresses, " ", ""))[count.index + 2]
    network_prefix = var.network_prefix
    gw4 = var.gateway4
    dns = var.nameservers
  }
}

data "template_file" "nfs_userdata_static" {
  template = file("${path.module}/userdata/nfs_static.userdata")
  count            = var.dhcp == false ? 1 : 0
  vars = {
    password      = var.ubuntu_password == null ? random_string.ubuntu_password.result : var.ubuntu_password
    pubkey        = chomp(tls_private_key.ssh.public_key_openssh)
    netplanFile = var.nfs.netplanFile
    hostname = "${var.nfs.basename}${count.index}-${random_string.id.result}"
    network_config  = base64encode(data.template_file.network_nfs[count.index].rendered)
  }
}

data "template_file" "nfs_userdata_dhcp" {
  template = file("${path.module}/userdata/nfs_dhcp.userdata")
  count            = var.dhcp == true ? 1 : 0
  vars = {
    password      = var.ubuntu_password == null ? random_string.ubuntu_password.result : var.ubuntu_password
    pubkey        = chomp(tls_private_key.ssh.public_key_openssh)
    hostname = "${var.nfs.basename}${count.index}-${random_string.id.result}"
  }
}

resource "vsphere_virtual_machine" "nfs" {
  count            = 1
  name             = "${var.nfs.basename}${count.index}-${random_string.id.result}"
  datastore_id     = data.vsphere_datastore.datastore.id
  resource_pool_id = data.vsphere_resource_pool.pool.id
  network_interface {
    network_id = data.vsphere_network.network.id
  }

  num_cpus = var.nfs.cpu
  memory = var.nfs.memory
  wait_for_guest_net_routable = var.nfs.wait_for_guest_net_routable
  guest_id = "ubuntu64Guest"

  disk {
    size             = var.nfs.disk
    label            = "${var.nfs.basename}.lab_vmdk"
    thin_provisioned = true
  }

  cdrom {
    client_device = true
  }

  clone {
    template_uuid = vsphere_content_library_item.file.id
  }

  vapp {
    properties = {
      hostname    = "${var.nfs.basename}${count.index}-${random_string.id.result}"
      public-keys = chomp(tls_private_key.ssh.public_key_openssh)
      user-data   = var.dhcp == true ? base64encode(data.template_file.nfs_userdata_dhcp[count.index].rendered) : base64encode(data.template_file.nfs_userdata_static[count.index].rendered)
    }
  }

  connection {
    host        = var.dhcp == true ? self.default_ip_address : split(",", replace(var.nodes_ip4_addresses, " ", ""))[count.index + 2]
    type        = "ssh"
    agent       = false
    user        = var.nfs.username
    private_key = tls_private_key.ssh.private_key_pem
  }

  provisioner "remote-exec" {
    inline      = [
      "while [ ! -f /tmp/cloudInitDone.log ]; do sleep 1; done"
    ]
  }
}